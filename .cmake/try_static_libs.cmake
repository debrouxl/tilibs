macro(try_static_libs_if_needed)
    if (TRY_STATIC_LIBS)
        if(NOT CMAKE_FIND_LIBRARY_PREFIXES)
            set(STATIC_LIB_PREFIX "")
        else()
            list(GET CMAKE_FIND_LIBRARY_PREFIXES 0 STATIC_LIB_PREFIX)
        endif()
        list(APPEND DEPS_STATIC_LIBRARY_DIRS "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
        if(APPLE)
            set(DYNAMIC_LIB_EXTS ".dylib" ".tbd")
            set(STATIC_LIB_EXT ".a")
            list(APPEND DEPS_LIBRARY_DIRS "/usr/lib" "${CMAKE_OSX_SYSROOT}/usr/lib" "/opt/homebrew/lib" "/usr/local/opt" "/usr/local/lib" "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
            list(APPEND DEPS_STATIC_LIBRARY_DIRS "/opt/homebrew/lib" "/usr/local/opt" "/usr/local/lib" "${CMAKE_OSX_SYSROOT}/usr/lib" )
        elseif(UNIX)
            set(DYNAMIC_LIB_EXTS ".so")
            set(STATIC_LIB_EXT ".a")
        elseif(WIN32 OR MINGW)
            set(DYNAMIC_LIB_EXTS ".dll")
            set(STATIC_LIB_EXT ".lib")
        endif()
        list(REMOVE_DUPLICATES DEPS_STATIC_LIBRARIES)
        list(REMOVE_DUPLICATES DEPS_STATIC_LIBRARY_DIRS)
        set(TRY_STATIC_DEPS_LDFLAGS "")
        foreach(LIB_NAME ${DEPS_STATIC_LIBRARIES})
            set(LIB_NAME_FULL ${LIB_NAME})
            foreach(LIB_DIR ${DEPS_STATIC_LIBRARY_DIRS})
                if(NOT EXISTS ${LIB_DIR})
                    continue()
                endif()
                # there's a weird libarchive/zlib issue with static on linux right now:
                #   /usr/lib/x86_64-linux-gnu/libarchive.a(archive_read_support_filter_gzip.o): undefined reference to symbol 'inflateEnd'
                #   /lib/x86_64-linux-gnu/libz.so.1: error adding symbols: DSO missing from command line
                if(LIB_NAME STREQUAL "archive" AND CMAKE_SYSTEM_NAME STREQUAL "Linux" AND CMAKE_CXX_COMPILER_ID MATCHES "GNU")
                    message(WARNING "[GCC Linux] Skipping libarchive static lib search due to a linking issue, will fallback on the shared lib")
                    break()
                endif()
                set(LIB_FULLPATH "${LIB_DIR}/${STATIC_LIB_PREFIX}${LIB_NAME}${STATIC_LIB_EXT}")
                if(NOT EXISTS ${LIB_FULLPATH})
                    set(LIB_FULLPATH "${LIB_DIR}/${LIB_NAME_FULL}/lib/${STATIC_LIB_PREFIX}${LIB_NAME}-static${STATIC_LIB_EXT}")
                endif()
                if(NOT EXISTS ${LIB_FULLPATH})
                    set(LIB_FULLPATH "${LIB_DIR}/lib${LIB_NAME_FULL}/lib/${STATIC_LIB_PREFIX}${LIB_NAME}${STATIC_LIB_EXT}")
                endif()
                if(NOT EXISTS ${LIB_FULLPATH})
                    if(LIB_NAME_FULL STREQUAL "bz2")
                        set(LIB_NAME_FULL "bzip2")
                    elseif(LIB_NAME_FULL STREQUAL "z")
                        set(LIB_NAME_FULL "zlib")
                    endif()
                    set(LIB_FULLPATH "${LIB_DIR}/${LIB_NAME_FULL}/lib/${STATIC_LIB_PREFIX}${LIB_NAME}${STATIC_LIB_EXT}")
                endif()
                if(EXISTS ${LIB_FULLPATH})
                    message("[Info] Found static lib '${LIB_NAME}' for '${PROJECT_NAME}': ${LIB_FULLPATH}")
                    list(APPEND TRY_STATIC_DEPS_LDFLAGS ${LIB_FULLPATH})
                    set(${LIB_NAME}_FOUND YES)
                    break()
                endif()
            endforeach()
            if(NOT ${LIB_NAME}_FOUND)
                foreach(LIB_DIR ${DEPS_LIBRARY_DIRS})
                    if(NOT EXISTS ${LIB_DIR})
                        continue()
                    endif()
                    foreach(DYNAMIC_LIB_EXT ${DYNAMIC_LIB_EXTS})
                        set(LIB_FULLPATH "${LIB_DIR}/${STATIC_LIB_PREFIX}${LIB_NAME}${DYNAMIC_LIB_EXT}")
                        if(NOT EXISTS ${LIB_FULLPATH})
                            set(LIB_FULLPATH "${LIB_DIR}/${LIB_NAME_FULL}/lib/${STATIC_LIB_PREFIX}${LIB_NAME}${DYNAMIC_LIB_EXT}")
                        endif()
                        if(NOT EXISTS ${LIB_FULLPATH})
                            set(LIB_FULLPATH "${LIB_DIR}/lib${LIB_NAME_FULL}/lib/${STATIC_LIB_PREFIX}${LIB_NAME}${DYNAMIC_LIB_EXT}")
                        endif()
                        if(EXISTS ${LIB_FULLPATH})
                            list(PREPEND TRY_STATIC_DEPS_LDFLAGS "-l${LIB_NAME}")
                            set(${LIB_NAME}_FOUND YES)
                            message("[Warning] Could not find static lib '${LIB_NAME}' for '${PROJECT_NAME}', using found shared lib instead: ${LIB_FULLPATH}")
                            break()
                        endif()
                    endforeach()
                    if(${LIB_NAME}_FOUND)
                        break()
                    endif()
                endforeach()
                if(NOT ${LIB_NAME}_FOUND)
                    message("[Warning] Could not find static nor shared lib '${LIB_NAME}' for '${PROJECT_NAME}'. Adding '-l${LIB_NAME}' directly anyway!")
                    list(APPEND TRY_STATIC_DEPS_LDFLAGS "-l${LIB_NAME}")
                endif()
            endif()
        endforeach()
        foreach(LIB_DIR ${DEPS_LIBRARY_DIRS})
            if(EXISTS ${LIB_DIR})
                list(PREPEND TRY_STATIC_DEPS_LDFLAGS "-L${LIB_DIR}")
            endif()
        endforeach()
        string(REPLACE ";" " " DEPS_STATIC_LDFLAGS_OTHER "${DEPS_STATIC_LDFLAGS_OTHER}")
        list(APPEND TRY_STATIC_DEPS_LDFLAGS "${DEPS_STATIC_LDFLAGS_OTHER}")
        list(REMOVE_DUPLICATES TRY_STATIC_DEPS_LDFLAGS)
    endif()
endmacro()
